assert hasProperty('OTNUsername'): "Set the property 'OTNUsername' in your your gradle.properties to download the OJDBC jar from Oracle's secured repository"
assert hasProperty('OTNPassword'): "Set the property 'OTNPassword' in your your gradle.properties to download the OJDBC jar from Oracle's secured repository"

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.5.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

bootJar {
    baseName = 'NVCLDataServices'
    version =  '2.1.0'
    launchScript()
}

repositories {
    mavenCentral()
    
    maven {

       url "https://www.oracle.com/content/secure/maven/content"

       credentials {
         username = OTNUsername
         password = OTNPassword
       }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
	compile("org.springframework.boot:spring-boot-starter-activemq")
	compile("org.springframework.boot:spring-boot-starter-jdbc")
	compile("org.springframework.boot:spring-boot-starter-quartz")
    compile("org.springframework.boot:spring-boot-starter-mail")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.apache.activemq:activemq-broker")
    compile group: 'org.apache.activemq', name:'activemq-kahadb-store'
	compile group: 'commons-io', name: 'commons-io', version: '2.6'
	compile group: 'org.jfree', name: 'jfreechart', version: '1.0.19'
	compile group: 'com.drewnoakes', name: 'metadata-extractor', version: '2.10.0'
	compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.0'
	compile group: 'au.com.bytecode', name: 'opencsv', version: '2.4'
	compile group: 'org.springframework', name: 'spring-oxm', version: '5.0.9.RELEASE'
    compile 'com.oracle.jdbc:ojdbc7:12.1.0.2'
    compile group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: '7.0.0.jre8'
	testCompile 'org.jmock:jmock-junit4:2.6.0'
	testCompile 'org.jmock:jmock-legacy:2.6.0'
    testCompile("junit:junit")
}

task runApp(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath

  main = 'org.auscope.nvcl.server.Application'

  // arguments to pass to the application
  // '-Dhttp.proxyHost=127.0.0.1',  '-Dhttp.proxyPort=8888',  // forward connections through fiddler proxy
  jvmArgs  "-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005"
}
